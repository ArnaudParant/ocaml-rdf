<page title="Using graphs"
navbar-doc="active"
with-contents="true"
>
<contents>
<prepare-toc>
<toc/>
<section id="creategraph" title="Creating a graph">
<p>
An RDF graph needs a base URI, which can be used to name the graph.
Such a URI is created using the <refdoc mod="Rdf_uri"/> module.
Then we use the <refdoc mod="Rdf_graph" id="VALopen_graph" name="Rdf_graph.open_graph"/>
function to create an empty graph:
</p>
<ocaml-eval>
<![CDATA[let base_uri = Rdf_uri.uri "http://foo.bar.net";;
let g = Rdf_graph.open_graph base_uri ;;]]>
</ocaml-eval>
<p>
If no option is provided, the graph is created in memory.
You can <elt href="doc-databases">create graphs stored in a MySQL or Postgresql
datatabase</elt>.
</p>
<p>
Once the graph is created, let's insert triples in the graph,
to associate the name "Alice" and the mailbox address URI
"mailto:alice@mbox.net" to a new blank node:
</p>
<ocaml-eval toplevel="true">
let foaf_name = Rdf_uri.uri "http://xmlns.com/foaf/0.1/name";;
let foaf_mbox = Rdf_uri.uri "http://xmlns.com/foaf/0.1/mbox";;

let a = Rdf_node.Blank_ (g.Rdf_graph.new_blank_id());;
g.Rdf_graph.add_triple
   ~sub: a ~pred: (Rdf_node.Uri foaf_name)
   ~obj: (Rdf_node.node_of_literal_string "Alice");;

g.Rdf_graph.add_triple
   ~sub: a ~pred: (Rdf_node.Uri foaf_mbox)
   ~obj: (Rdf_node.Uri (Rdf_uri.uri "mailto:alice@mbox.net"));;

let b = Rdf_node.Blank_ (g.Rdf_graph.new_blank_id());;
g.Rdf_graph.add_triple
   ~sub: b ~pred: (Rdf_node.Uri foaf_name)
   ~obj: (Rdf_node.node_of_literal_string "Bob");;

g.Rdf_graph.add_triple
   ~sub: b ~pred: (Rdf_node.Uri foaf_mbox)
   ~obj: (Rdf_node.Uri (Rdf_uri.uri "mailto:bob@mbox.net"));;
</ocaml-eval>
<p>Here is the content of our graph, in Turtle format:</p>
<showttl graph="g"/>
</section>
<section id="querying" title="Querying the graph">
<p>
Let's use some of the <reftyp mod="Rdf_graph" name="graph"/> structure fields to
retrieve information from the graph.
</p>
<p>Here we look for the mailboxes of Alice, by querying the objects of
triples having a specified subject and object:
</p>
<ocaml-eval toplevel="true">
let alice_mbox = g.Rdf_graph.objects_of
  ~sub: a ~pred: (Rdf_node.Uri foaf_mbox);;
List.iter (fun n -> print_endline (Rdf_node.string_of_node n)) alice_mbox;;
</ocaml-eval>
<p>
Let's now query all triples having the predicate <code>foaf_name</code>,
and use the <refval mod="Rdf_ttl" name="string_of_triple"/> function
to display the result:
</p>
<ocaml-eval toplevel="true">
let triples = g.Rdf_graph.find ~pred: (Rdf_node.Uri foaf_name) ();;
List.iter
   (fun (sub,pred,obj) ->
     print_endline (Rdf_ttl.string_of_triple ~sub ~pred ~obj))
   triples;;
</ocaml-eval>
<p>These are simple ways to query the graph.
<page href="doc-sparql">Sparql queries can be used</page> to execute
more powerful and more complex queries.
</p>
</section>

<section id="import" title="Importing and exporting">
<p>
The <ext-a href="http://www.w3.org/TR/rdf-syntax-grammar">RDF/XML</ext-a> and
<ext-a href="http://www.w3.org/TeamSubmission/turtle/">Turtle</ext-a> formats
are supported to import and export triples.
The corresponding functions are provided in modules
<refmod mod="Rdf_xml"/> and <refmod mod="Rdf_ttl"/>.
</p>
<p>Here is an example of code creating a graph, importing from a
RDF/XML file <code>example.rdf</code> and exporting to a Turtle file
<code>example.ttl</code>:
</p>
<ocaml-eval toplevel="true">
let base = Rdf_uri.uri "http://hello.fr" ;;
let g = Rdf_graph.open_graph base ;;
Rdf_xml.from_file g base "example.rdf";;
Rdf_ttl.to_file g "example.ttl";;
</ocaml-eval>
</section>

<section id="transactions" title="Transactions">
<p>
Transactions can be used if the backend support them.
The storage in memory supports nested transactions, and database
backends support one level of transaction.
</p>
<p>Transactions are handled with the
<code>transaction_start</code>, <code>transaction_commit</code>
and <code>transaction_rollback</code> of the the
<reftyp mod="Rdf_graph" name="graph"/> structure.
</p>
<p>As an example, let's load data from a file:</p>
<ocaml-eval>
let base = Rdf_uri.uri "http://hello.fr" ;;
let g = Rdf_graph.open_graph base ;;
Rdf_ttl.from_file g base "example.ttl";;
</ocaml-eval>
<p>This is our loaded data:</p>
<showttl graph="g"/>
<p>Now let's start a transaction, and modify the graph:</p>
<ocaml-eval>
g.Rdf_graph.transaction_start();;
let pred = Rdf_node.Uri (Rdf_uri.uri "http://example.org/stuff/1.0/fullName") ;;
match g.Rdf_graph.find ~pred () with
| (sub,_,obj) :: _ ->
  g.Rdf_graph.rem_triple ~sub ~pred ~obj;
  g.Rdf_graph.add_triple ~sub ~pred
    ~obj: (Rdf_node.node_of_literal_string "Jean Dupont")
| _ -> assert false;;
</ocaml-eval>
<p>Now the graph looks like:</p>
<showttl graph="g"/>
<p>If we rollback, we get the previous graph:</p>
<ocaml-eval>g.Rdf_graph.transaction_rollback();;</ocaml-eval>
<showttl graph="g"/>
</section>
</prepare-toc>
</contents>
</page>